
# Multidimensional array class


load: fus arrtools
load: fus funtools
use: arrtools resize
use: funtools map
use: funtools all
use: funtools prod


module Array:

    def new of(dims -> Array):
        ='dims
        (obj arr =.dims arr =.data) 'dims @redim

    def redim of(Array dims -> Array):
        dup fun(is_int) @map @all assert
        ='new_dims
        'new_dims =.dims
        ..data ('new_dims @prod) @resize =.data

    def get_i of(Array v -> i):
        # transform v (an arr of int indices for individual dimensions) into i
        # (an int index into a data arr)
        ='v ='array
        'array .dims ='dims
        0 ='i

        'v len @int_togen for:
            ''i
                'dims 'v_i .$ *
                'v 'v_i .$ +
            ='i

        ''i

    def get of(Array v -> value):
        ='v ='array
        'array 'v @get_i ='i
        'array .data 'i .$

    def set of(Array value v -> Array):
        ='v ='value ='array
        'array 'v @get_i ='i
        ''array ..data 'value 'i =.$ =.data

    def rip of(Array v -> Array value):
        ='v ='array
        'array 'v @get_i ='i
        'array .data 'i ..$ ='value =.data
        ''value

    def test of(->):
        (arr 2, 3,) @new
            dup (arr 0, 0,) @get is_null assert

            "HA" (arr 0, 0,) @set
            "HO" (arr 1, 0,) @set
            "HI" (arr 0, 1,) @set

            dup (arr 0, 0,) @get "HA" str_eq assert
            dup (arr 1, 0,) @get "HO" str_eq assert
            dup (arr 0, 1,) @get "HI" str_eq assert

            dup (arr 0, 0,) @rip "HA" str_eq assert
                dup (arr 0, 0,) @get is_null assert
            drop
        drop

