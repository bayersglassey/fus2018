
# Multidimensional array class


load: fus arrtools
load: fus funtools
use: arrtools resize
use: funtools map
use: funtools all
use: funtools prod


module Array:

    def
    sig: dims -> Array
    new:
        ='dims
        'dims fun(is_int) @map @all assert
        (obj arr =.dims arr =.data) 'dims @redim

    def
    sig: Array dims -> Array
    redim:
        ='new_dims
        'new_dims =.dims
        ..data ('new_dims @prod) @resize =.data

    def
    sig: Array v -> i
    get_i:
        # transform v (an arr of int indices for individual dimensions) into i
        # (an int index into a data arr)
        ='v ='array
        'array .dims ='dims
        0 ='i

        'v len @int_togen for:
            ''i
                'dims 'v_i .$ *
                'v 'v_i .$ +
            ='i

        ''i

    def
    sig: Array v -> value
    get:
        ='v ='array
        'array 'v @get_i ='i
        'array .data 'i .$

    def
    sig: Array value v -> Array
    set:
        ='v ='value ='array
        'array 'v @get_i ='i
        ''array ..data 'value 'i =.$ =.data

    def
    sig: Array v -> Array value
    rip:
        ='v ='array
        'array 'v @get_i ='i
        'array .data 'i ..$ ='value =.data
        ''value

    def
    sig(->)
    test:
        (arr 2, 3,) @new
            dup (arr 0, 0,) @get is_null assert

            "HA" (arr 0, 0,) @set
            "HO" (arr 1, 0,) @set
            "HI" (arr 0, 1,) @set

            dup (arr 0, 0,) @get "HA" str_eq assert
            dup (arr 1, 0,) @get "HO" str_eq assert
            dup (arr 0, 1,) @get "HI" str_eq assert

            dup (arr 0, 0,) @rip "HA" str_eq assert
                dup (arr 0, 0,) @get is_null assert
            drop
        drop

