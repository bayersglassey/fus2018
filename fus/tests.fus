
stack len 0 == assert

"Stack" p
1 dup 1 == assert 1 == assert
1 2 drop 1 == assert
1 2 swap 1 == assert 2 == assert
1 2 nip 2 == assert
1 2 over 1 == assert 2 == assert 1 == assert
"ok" p

stack len 0 == assert

"Vars" p
1 ='x
'x 1 == assert
''x 1 == assert
'x is null assert
"ok" p

stack len 0 == assert

"Arithmetic" p
10 neg -10 == assert
1 2 +    3 == assert
1 2 -   -1 == assert
2 3 *    6 == assert
6 3 /    2 == assert
7 3 mod  1 == assert
# TODO: tests for "/" and "%" when there are negatives involved
3 4 == not assert
3 4 != assert
3 4 <  assert
3 4 <= assert
3 4 >  not assert
3 4 >= not assert
"ok" p

stack len 0 == assert

"Blocks" p
: # empty block
"ok" p

stack len 0 == assert

"Logic" p
t assert
f not assert
t t and assert
t f and not assert
f t and not assert
f f and not assert
t t or assert
t f or assert
f t or assert
f f or not assert
:
    t ='cond
    f if: f ='cond
    ''cond assert
:
    t ='cond
    t if: f ='cond
    ''cond not assert
:
    t ifelse: t ='cond
    : f ='cond
    ''cond assert
:
    f ifelse: t ='cond
    : f ='cond
    ''cond not assert
def which_nested_conditional_branch(b b -> i):
    # Returns 0, 1, 2, or 3 depending on which branch was reached
    ifelse:
        ifelse: 0 ='x
        : 1 ='x
    :
        ifelse: 2 ='x
        : 3 ='x
    ''x
t t @which_nested_conditional_branch 0 == assert
f t @which_nested_conditional_branch 1 == assert
t f @which_nested_conditional_branch 2 == assert
f f @which_nested_conditional_branch 3 == assert
"ok" p

stack len 0 == assert

"Arrays" p
arr 1, 2, 3, ='a
'a 0 .$ 1 == assert
'a 1 .$ 2 == assert
'a
    20 1 =.$
    pop drop
    pop 20 == assert
drop
'a len 3 == assert
'a lpop 1 == assert len 2 == assert
'a pop 3 == assert len 2 == assert
"ok" p

stack len 0 == assert

"Objects" p
obj 1 =.x 2 `y set
    dup keys len 2 == assert
    dup ?.x assert
    dup ?.z not assert
    dup `x get 1 == assert
    dup `y rip 2 == assert .y is null assert
drop
"ok" p

stack len 0 == assert

"Loops" p
arr ='a
0 3 do loop:
    over over < while loop
    swap 1 + swap
    1 2 do loop:
        over over <= while loop
        swap
            ''a over, ='a
        1 + swap
    drop drop
drop drop
''a
    lpop 1 == assert
    lpop 2 == assert
    lpop 1 == assert
    lpop 2 == assert
    lpop 1 == assert
    lpop 2 == assert
len 0 == assert
"ok" p

stack len 0 == assert

"Data" p
data:
    1 2 do loop:
        over over <= while loop
        swap dup p 1 + swap
    drop drop
lpop 1 == assert
lpop 2 == assert
lpop `do sym_eq assert
lpop `loop sym_eq assert
lpop
    lpop `over sym_eq assert
    lpop `over sym_eq assert
    lpop ` <= sym_eq assert
    lpop `while sym_eq assert
    lpop `loop sym_eq assert
    lpop `swap sym_eq assert
    lpop `dup sym_eq assert
    lpop `p sym_eq assert
    lpop 1 == assert
    lpop ` + sym_eq assert
    lpop `swap sym_eq assert
len 0 == assert
lpop `drop sym_eq assert
lpop `drop sym_eq assert
len 0 == assert
"ok" p

stack len 0 == assert

"Modules & Functions" p
module ModuleTest:
    sig F: -> i

    def f F: 2

    def test(-> bool):
        fun F: 1
        ='f

        @f 2 == assert
        'f call F 1 == assert
        &f call F 2 == assert

        t
@(ModuleTest test) assert
use module: ModuleTest =MT
@(MT test) assert
use def: ModuleTest test =module_test
@module_test assert

"ok" p

stack len 0 == assert

"Math" p
load: fus math
@(math test)
"ok" p

stack len 0 == assert

"Class" p
load: fus class
@(class test)
"ok" p

stack len 0 == assert

"Format" p
load: fus format
@(format test)
"ok" p

stack len 0 == assert

"Iter" p
load: fus iter
@(iter test)
"ok" p

stack len 0 == assert

"Heap" p
load: fus heap
@(heap test)
"ok" p

stack len 0 == assert

"Cycle" p
load: fus cycle
@(cycle test)
"ok" p

stack len 0 == assert

"Objtools" p
load: fus objtools
@(objtools test)
"ok" p

stack len 0 == assert

"Arrtools" p
load: fus arrtools
@(arrtools test)
"ok" p

stack len 0 == assert

"Eq" p
load: fus eq
@(eq test)
"ok" p

stack len 0 == assert
