
def range(x0 x1 add -> xs):
    # Example: 0 3 1 @range -> arr 0, 1, 2,
    ='add
    arr ='a
    do loop:
        # (x0 x1)
        over over != while loop
        swap
            # (x1 x0)
            dup ''a swap push ='a
            # (x1 x0)
            'add +
        swap
    drop drop
    ''a


def resize(xs len -> xs):

    def min(x y -> z):
        ='y ='x
        'x 'y < ifelse('x)('y)

    ='new_len ='xs
    null 'new_len repeat ='new_xs
    0 ('xs len 'new_len @min) do loop:
        over over < while loop
        swap dup ='i 1 + swap

        ''new_xs ('xs 'i .$) 'i =.$ ='new_xs
    drop drop

    ''new_xs

def map(xs f -> xs):
    ='f ='xs
    0 'xs len do loop:
        over over < while loop
        swap dup ='i 1 + swap
        ''xs 'i ..$ 'f call(x -> y) 'i =.$ ='xs
    drop drop
    ''xs

def fold(xs x f -> x):
    ='f ='x ='xs
    0 'xs len do loop:
        over over < while loop
        swap dup ='i 1 + swap
        ''x ('xs 'i .$) 'f call(x y -> z) ='x
    drop drop
    ''x

def all(xs -> bool):
    t fun(x y -> z)(and) @fold

def any(xs -> bool):
    f fun(x y -> z)(or) @fold

def sum(xs -> bool):
    0 fun(x y -> z)(+) @fold

def prod(xs -> bool):
    1 fun(x y -> z)(*) @fold



def test(->):
    0 3 1 @range
    lpop 0 == assert
    lpop 1 == assert
    lpop 2 == assert
    len 0 == assert

    12 10 -1 @range
    lpop 12 == assert
    lpop 11 == assert
    len 0 == assert

    (arr 1, 2,) 4 @resize
        lpop 1 == assert
        lpop 2 == assert
        lpop is null assert
        lpop is null assert
    len 0 == assert

    (arr 1, 2, 3, 4,) 2 @resize
        lpop 1 == assert
        lpop 2 == assert
    len 0 == assert

    (arr 1, 2, 3,) fun(x->y)(10 *) @map
        lpop 10 == assert
        lpop 20 == assert
        lpop 30 == assert
    len 0 == assert

    (arr t, t, t,) @all assert
    (arr t, f, t,) @all not assert

    (arr t, f, t,) @any assert
    (arr f, f, f,) @any not assert

    (arr 1, 2, 3, 4,) @sum 10 == assert
    (arr 1, 2, 3, 4,) @prod 24 == assert


