
def map of(xs f ->xs):
    ='f ='xs
    'xs len int_for: ='i
        ''xs 'i ..$
            'f call(x -> y)
        'i =.$ ='xs
    ''xs

def fold of(xs x f -> x):
    ='f ='x ='xs
    'xs len int_for: ='i
        ''x ('xs 'i .$) 'f call(x y -> z) ='x
    ''x

def all of(xs -> bool):
    T fun(and) @fold

def any of(xs -> bool):
    F fun(or) @fold

def sum of(xs -> int):
    0 fun(+) @fold

def prod of(xs -> int):
    1 fun(*) @fold



def test of(->):

    (arr 1, 2, 3,) fun(10 *) @map
        lpop 10 == assert
        lpop 20 == assert
        lpop 30 == assert
    len 0 == assert

    (arr T, T, T,) @all assert
    (arr T, F, T,) @all not assert

    (arr T, F, T,) @any assert
    (arr F, F, F,) @any not assert

    (arr 1, 2, 3, 4,) @sum 10 == assert
    (arr 1, 2, 3, 4,) @prod 24 == assert


