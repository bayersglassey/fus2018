
def test of(->):
    arr "Thing ", 2, ": ", (obj 1 =.x 2 =.y), "!",
    @format_simple "Thing 2: {x: 1, y: 2}!" str_eq assert



def DEFAULT_OPTS of(-> opts):
    obj
        null =.max_obj_entries
        null =.max_arr_entries
def format_simple of(a -> s): @DEFAULT_OPTS @format
def tostr_simple of(x -> s): @DEFAULT_OPTS @tostr


def format of(a opts -> s):
    ='opts
    "" swap arr_for:
        'opts @tostr str_join

def tostr of(x opts -> s):
    ='opts
    obj
        fun of(x opts -> s)(drop drop "null") =.null
        fun of(x opts -> s)(drop ifelse("T")("F")) =.bool
        fun of(x opts -> s)(drop int_tostr) =.int
        fun of(x opts -> s)(drop) =.str
        fun of(x opts -> s)(drop sym_tostr) =.sym
        fun of(x opts -> s)(drop drop "<fun>") =.fun
        &obj_tostr =.obj
        &arr_tostr =.arr
    over typeof get ='tostr
    ''opts ''tostr call(x opts -> s)

def obj_tostr of(x opts -> s):
    ='opts
    ='o

    'o keys ='keys
    'opts .max_obj_entries ='max_entries
    "{" ='s

    'keys len int_for: ='i
        'max_entries is_int if:
            'i 'max_entries >= if:
                ''s ", ..." str_join ='s break
        'keys 'i .$ ='key
        'o 'key get ='val
        ''s
            'i 0 > if: ", " str_join
            'key sym_tostr str_join
            ": " str_join
            'val 'opts @tostr str_join
        ='s

    ''s "}" str_join

def arr_tostr of(x opts -> s):
    ='opts
    ='a

    'opts .max_arr_entries ='max_entries
    "[" ='s

    'a len int_for: ='i
        'max_entries is_int if:
            'i 'max_entries >= if:
                ''s ", ..." str_join ='s break
        'a 'i .$ ='val
        ''s
            'i 0 > if: ", " str_join
            'val 'opts @tostr str_join
        ='s

    ''s "]" str_join

@test
